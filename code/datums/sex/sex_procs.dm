/mob/living/proc/get_highest_grab_state_on(mob/living/victim)
	var/grabstate = null
	if(r_grab && r_grab.grabbed == victim)
		if(grabstate == null || r_grab.grab_state > grabstate)
			grabstate = r_grab.grab_state
	if(l_grab && l_grab.grabbed == victim)
		if(grabstate == null || l_grab.grab_state > grabstate)
			grabstate = l_grab.grab_state
	return grabstate

/mob/living/proc/get_effective_grab_state_on(mob/living/victim)
	var/grabstate = get_highest_grab_state_on(victim)
	if(pulling != victim && victim?.pulledby != src)
		return grabstate
	if(isnull(grabstate))
		return grab_state
	return max(grabstate, grab_state)

/proc/do_thrust_animate(atom/movable/user, atom/movable/target, pixels = 4, time = 2.7)
	var/datum/sex_session/sex_session
	if(isliving(user) && isliving(target))
		sex_session = get_sex_session(user, target)
		if(!sex_session)
			sex_session = get_sex_session(target, user)
	if(sex_session)
		if(sex_session.speed > SEX_SPEED_MID)
			time = max(0.5, time - 0.25)
		if(sex_session.force < SEX_FORCE_MID)
			pixels = max(1, pixels - 1)
	var/oldx = user.pixel_x
	var/oldy = user.pixel_y
	var/target_x = oldx
	var/target_y = oldy
	var/dir = get_dir(user, target)
	if(user.loc == target.loc)
		dir = user.dir
	switch(dir)
		if(NORTH)
			target_y += pixels
		if(SOUTH)
			target_y -= pixels
		if(WEST)
			target_x -= pixels
		if(EAST)
			target_x += pixels

	animate(user, pixel_x = target_x, pixel_y = target_y, time = time)
	animate(pixel_x = oldx, pixel_y = oldy, time = time)

/mob/living/proc/start_sex_session(mob/living/target, show_ui = TRUE)
	if(!target)
		return
	if(QDELETED(src) || QDELETED(target))
		return
	if(stat == DEAD || target.stat == DEAD)
		return
	if(src != target && !target.allows_player_erp_while_disconnected())
		return
	if(!GetComponent(/datum/component/arousal))
		AddComponent(/datum/component/arousal)
	if(!target.GetComponent(/datum/component/arousal))
		target.AddComponent(/datum/component/arousal)
	var/datum/sex_session/old_session = get_sex_session(src, target)
	if(old_session && !QDELETED(old_session))
		if(show_ui)
			old_session.show_ui()
		return old_session


	var/datum/sex_session/session = new /datum/sex_session(src, target)
	register_sex_session(session)
	if(target.client && client && show_ui)
		session.show_ui()
	return session

/proc/register_sex_session(datum/sex_session/session)
	if(!session)
		return
	LAZYADD(GLOB.sex_sessions, session)
	add_user_sex_session(session.user, session)
	if(session.target != session.user)
		add_user_sex_session(session.target, session)

/proc/add_user_sex_session(mob/living/user, datum/sex_session/session)
	if(!user || !session)
		return
	var/list/user_sessions = GLOB.sex_sessions_by_user[user]
	if(!user_sessions)
		user_sessions = list()
		GLOB.sex_sessions_by_user[user] = user_sessions
	user_sessions |= session

/proc/unregister_sex_session(datum/sex_session/session)
	if(!session)
		return
	GLOB.sex_sessions -= session
	remove_user_sex_session(session.user, session)
	if(session.target != session.user)
		remove_user_sex_session(session.target, session)

/proc/remove_user_sex_session(mob/living/user, datum/sex_session/session)
	if(!user || !session)
		return
	var/list/user_sessions = GLOB.sex_sessions_by_user[user]
	if(!user_sessions)
		return
	user_sessions -= session
	if(length(user_sessions))
		return
	GLOB.sex_sessions_by_user -= user

/mob/living/proc/make_sucking_noise()
	var/suckyvolume = 25
	if(rogue_sneaking || alpha <= 100)
		suckyvolume *= 0.5
	if(gender == FEMALE)
		playsound(src, pick('sound/misc/mat/girlmouth (1).ogg','sound/misc/mat/girlmouth (2).ogg'), suckyvolume, TRUE, ignore_walls = FALSE)
	else
		playsound(src, pick('sound/misc/mat/guymouth (2).ogg','sound/misc/mat/guymouth (3).ogg','sound/misc/mat/guymouth (4).ogg','sound/misc/mat/guymouth (5).ogg'), suckyvolume, TRUE, ignore_walls = FALSE)

/mob/living/proc/can_do_sex()
	return TRUE

/mob/living/MiddleMouseDrop_T(atom/movable/dragged, mob/living/user)
	var/mob/living/target = src

	if(user.mmb_intent)
		return ..()
	if(!istype(dragged))
		return
	// Need to drag yourself to the target.
	if(dragged != user)
		return
	if(!user.can_do_sex())
		to_chat(user, "<span class='warning'>I can't do this.</span>")
		return

	if(!user.start_sex_session(target))
		to_chat(user, "<span class='warning'>I'm already sexing.</span>")
		return

/proc/get_sex_session(mob/giver, mob/taker)
	for(var/datum/sex_session/session as anything in return_sessions_with_user(giver))
		if(session.user != giver)
			continue
		if(session.target != taker)
			continue
		return session
	return null

/proc/get_or_create_sex_session(mob/living/giver, mob/living/taker, show_ui = FALSE)
	if(!giver || !taker)
		return null

	var/datum/sex_session/session = get_sex_session(giver, taker)
	if(session && !QDELETED(session))
		if(show_ui)
			session.show_ui()
		return session

	return giver.start_sex_session(taker, show_ui)

/mob/living/proc/has_hands()
	return TRUE

/mob/living/proc/has_free_sex_hands()
	return has_hands() && !HAS_TRAIT(src, TRAIT_HANDS_BLOCKED)

/mob/living/proc/has_mouth()
	return TRUE

/mob/living/proc/has_penis()
	return getorganslot(ORGAN_SLOT_PENIS) || gender == MALE

/mob/living/proc/has_testicles()
	return getorganslot(ORGAN_SLOT_TESTICLES) || gender == MALE

/mob/living/proc/has_vagina()
	return getorganslot(ORGAN_SLOT_VAGINA) || gender == FEMALE

/mob/living/proc/has_breasts()
	return getorganslot(ORGAN_SLOT_BREASTS) || gender == FEMALE

/mob/living/proc/has_belly()
	RETURN_TYPE(/obj/item/organ/genitals/belly)
	return getorganslot(ORGAN_SLOT_BELLY)

/mob/living/proc/has_butt()
	RETURN_TYPE(/obj/item/organ/genitals/butt)
	return getorganslot(ORGAN_SLOT_BUTT)

/mob/living/proc/is_fertile()
	var/obj/item/organ/genitals/filling_organ/vagina/vagina = getorganslot(ORGAN_SLOT_VAGINA)
	return vagina?.fertility

/mob/living/proc/is_virile()
	var/obj/item/organ/genitals/filling_organ/testicles/testicles = getorganslot(ORGAN_SLOT_TESTICLES)
	return testicles?.virility

/mob/living/carbon/human/has_penis()
	return getorganslot(ORGAN_SLOT_PENIS)

/mob/living/carbon/human/has_testicles()
	return getorganslot(ORGAN_SLOT_TESTICLES)

/mob/living/carbon/human/has_vagina()
	return getorganslot(ORGAN_SLOT_VAGINA)

/mob/living/carbon/human/has_breasts()
	RETURN_TYPE(/obj/item/organ/genitals/filling_organ/breasts)
	return getorganslot(ORGAN_SLOT_BREASTS)

/mob/living/proc/mouth_is_free()
	return has_mouth() && !mouth_blocked && !is_muzzled() && !is_mouth_covered()

/mob/living/proc/get_worn_choker()
	return null

/mob/living/carbon/get_worn_choker()
	if(istype(choker, /obj/item/clothing/choker))
		return choker
	if(istype(wear_neck, /obj/item/clothing/choker))
		return wear_neck
	return null

/mob/living/proc/snap_worn_choker(mob/living/puller)
	var/obj/item/clothing/choker/worn_choker = get_worn_choker()
	if(!worn_choker)
		return FALSE

	if(worn_choker.loc == src)
		dropItemToGround(worn_choker, TRUE, TRUE)

	if(worn_choker.break_sound)
		playsound(src, 'modular_rmh/sound/effects/snap.ogg', 30, TRUE, -1)

	visible_message(
		span_warning("[puller ? "[puller] thrusts hard enough to snap [src]'s [worn_choker.name]!" : "[src]'s [worn_choker.name] snaps!"]"),
		span_warning("[puller ? "[puller] thrusts hard enough to snap my [worn_choker.name]!" : "My [worn_choker.name] snaps!"]")
	)

	worn_choker.atom_break()

	return TRUE

/mob/living/proc/foot_is_free()
	return is_barefoot()

/mob/living/proc/is_barefoot()
	for(var/item_slot in DEFAULT_SLOT_PRIORITY)
		var/obj/item/clothing = get_item_by_slot(item_slot)
		if(!clothing) // Don't have this slot or not wearing anything in it
			continue
		if(clothing.body_parts_covered & FEET)
			return FALSE
	// If didn't stop before, then we're barefoot
	return TRUE

/mob/living/carbon/human/has_mouth()
	return get_bodypart(BODY_ZONE_HEAD)

/mob/living/carbon/human/has_hands() // technically should be an and but i'll replicate original behavior
	return get_bodypart(BODY_ZONE_L_ARM) || get_bodypart(BODY_ZONE_R_ARM)

/mob/living/proc/get_character_information_line(summary_text)
	if(!length("[summary_text]"))
		return null
	summary_text = "[summary_text]"
	var/last_character = copytext(summary_text, length(summary_text), length(summary_text) + 1)
	if(!(last_character in list(".", "!", "?")))
		summary_text += "."
	return "<div>...[html_encode("[summary_text]")]</div>"

/mob/living/proc/get_general_sex_state_summary()
	var/datum/component/arousal/arousal_component = GetComponent(/datum/component/arousal)
	if(!arousal_component)
		return null

	var/list/arousal_data = list()
	SEND_SIGNAL(src, COMSIG_SEX_GET_AROUSAL, arousal_data)

	var/current_arousal = arousal_data["arousal"] || 0
	var/current_orgasm_progress = arousal_data["orgasm_progress"] || 0
	var/state_summary = "feel calm"

	switch(current_arousal)
		if(1 to 19)
			state_summary = "feel a little worked up"
		if(20 to 49)
			state_summary = "feel lightly aroused"
		if(50 to 79)
			state_summary = "feel hot and needy"
		if(80 to INFINITY)
			state_summary = "feel desperately aroused"

	if(arousal_component.is_spent())
		state_summary += ", but spent from recent release"

	if(current_orgasm_progress >= PASSIVE_EJAC_THRESHOLD)
		state_summary += " and right on the brink"
	else if(current_orgasm_progress >= PASSIVE_EJAC_THRESHOLD * 0.7)
		state_summary += " and close to climax"

	return "[state_summary]."

/mob/living/proc/return_character_information()
	var/list/data = list()
	if(has_hands())
		data += get_character_information_line("have hands, which are [has_free_sex_hands() ? "free" : "blocked"]")
	if(has_mouth())
		data += get_character_information_line("have a mouth, which is [mouth_is_free() ? "uncovered" : "covered"]")
	var/general_state_summary = get_general_sex_state_summary()
	if(general_state_summary)
		data += get_character_information_line(general_state_summary)
	return data

/mob/living/carbon/human/proc/get_visible_groin_summary_description(descriptor_type)
	if(!get_location_accessible(src, BODY_ZONE_PRECISE_GROIN, skipundies = FALSE))
		return null
	var/datum/mob_descriptor/descriptor = new descriptor_type()
	return descriptor.get_description(src)

/mob/living/carbon/human/proc/get_visible_chest_summary_description(descriptor_type)
	if(!get_location_accessible(src, BODY_ZONE_CHEST))
		return null
	if(underwear?.covers_breasts)
		return null
	var/datum/mob_descriptor/descriptor = new descriptor_type()
	return descriptor.get_description(src)

/mob/living/carbon/human/proc/get_visible_anus_summary()
	var/obj/item/organ/genitals/filling_organ/anus/anus = getorganslot(ORGAN_SLOT_ANUS)
	if(!anus)
		return null
	if(!get_location_accessible(src, BODY_ZONE_PRECISE_GROIN, skipundies = FALSE))
		return null
	return "have an exposed anus."

/mob/living/carbon/human/return_character_information()
	var/list/data = ..()
	if(!data)
		data = list()

	var/breasts_summary = get_visible_chest_summary_description(/datum/mob_descriptor/breasts)
	if(breasts_summary)
		data += get_character_information_line("have [breasts_summary]")

	var/obj/item/organ/genitals/penis/penis = getorganslot(ORGAN_SLOT_PENIS)
	var/penis_summary = get_visible_groin_summary_description(/datum/mob_descriptor/penis)
	if(penis_summary)
		data += get_character_information_line("have [penis_summary]")

	var/testicles_summary = null
	if(!penis || penis.sheath_type != SHEATH_TYPE_SLIT)
		testicles_summary = get_visible_groin_summary_description(/datum/mob_descriptor/testicles)
	if(testicles_summary)
		data += get_character_information_line("have [testicles_summary]")

	var/vagina_summary = get_visible_groin_summary_description(/datum/mob_descriptor/vagina)
	if(vagina_summary)
		data += get_character_information_line("have [vagina_summary]")

	var/butt_summary = get_visible_groin_summary_description(/datum/mob_descriptor/butt)
	if(butt_summary)
		data += get_character_information_line("have [butt_summary]")

	var/anus_summary = get_visible_anus_summary()
	if(anus_summary)
		data += get_character_information_line(anus_summary)

	return data

/mob/living/proc/get_active_precise_hand()
	var/active_hand = BODY_ZONE_PRECISE_L_HAND
	if(active_hand_index != 1)
		active_hand = BODY_ZONE_PRECISE_R_HAND
	return active_hand

/mob/living/proc/get_inactive_precise_hand()
	if(get_active_precise_hand() == BODY_ZONE_PRECISE_L_HAND)
		return BODY_ZONE_PRECISE_R_HAND
	return BODY_ZONE_PRECISE_L_HAND

/mob/proc/check_handholding()
	return

/mob/living/carbon/human/check_handholding()
	if(pulledby && pulledby != src)
		var/obj/item/bodypart/LH
		var/obj/item/bodypart/RH
		LH = get_bodypart(BODY_ZONE_PRECISE_L_HAND)
		RH = get_bodypart(BODY_ZONE_PRECISE_R_HAND)
		if(LH || RH)
			for(var/obj/item/grabbing/G in src.grabbedby)
				if(G.limb_grabbed == LH || G.limb_grabbed == RH)
					return TRUE

/proc/return_sessions_with_user(mob/living/user)
	if(!user)
		return list()
	return GLOB.sex_sessions_by_user[user] || list()

/proc/return_highest_priority_action(list/sessions = list(), mob/living/user)
	var/datum/sex_session/highest_session
	for(var/datum/sex_session/session in sessions)
		var/datum/sex_action/session_action = session.get_highest_priority_action_for(user)
		if(!session_action)
			continue
		if(!highest_session)
			highest_session = session
			continue
		var/datum/sex_action/highest_action = highest_session.get_highest_priority_action_for(user)
		if(user == session.target)
			if(session_action.target_priority > highest_action.target_priority)
				highest_session = session
				continue
		if(user == session.user)
			if(session_action.user_priority > highest_action.user_priority)
				highest_session = session
				continue
	return highest_session

/mob/proc/get_erp_pref(pref_type)
	if(!ispath(pref_type, /datum/erp_preference))
		return FALSE

	if(isliving(src))
		var/mob/living/living_mob = src
		return living_mob.get_cached_erp_pref(pref_type)

	if(!client?.prefs)
		return FALSE

	var/datum/erp_preference/pref = new pref_type()
	return pref.get_value(client.prefs)

/mob/proc/set_erp_pref(pref_type, value)
	if(!client?.prefs)
		return FALSE

	if(!ispath(pref_type, /datum/erp_preference))
		return FALSE

	var/datum/erp_preference/pref = new pref_type()
	pref.set_value(client.prefs, value)
	client.prefs.save_preferences()
	return TRUE

/mob/proc/has_erp_pref(pref_type)
	return get_erp_pref(pref_type) == TRUE

/proc/should_apply_mob_erp_target_pref(mob/living/actor, mob/living/target)
	if(!isliving(actor) || !isliving(target))
		return FALSE
	if(actor.client)
		return FALSE
	if(target.client || target.mind?.key || target.mind?.cached_erp_preferences || target.cached_erp_preferences)
		return TRUE
	return FALSE

/proc/target_allows_mob_erp_action(mob/living/actor, mob/living/target, pref_type)
	if(!ispath(pref_type, /datum/erp_preference))
		return TRUE
	if(!should_apply_mob_erp_target_pref(actor, target))
		return TRUE
	return target.get_cached_erp_pref(pref_type)

/mob/proc/get_all_erp_prefs()
	if(!client?.prefs)
		return list()

	var/list/prefs_by_category = list()

	for(var/pref_type in subtypesof(/datum/erp_preference))
		var/datum/erp_preference/pref = new pref_type()
		var/category = pref.category
		var/value = pref.get_value(client.prefs)

		if(!prefs_by_category[category])
			prefs_by_category[category] = list()

		prefs_by_category[category][pref_type] = list(
			"name" = pref.name,
			"description" = pref.description,
			"value" = value,
			"pref_object" = pref
		)

	return prefs_by_category

/proc/any_has_erp_pref(list/mobs, pref_type)
	for(var/mob/M in mobs)
		if(M.has_erp_pref(pref_type))
			return TRUE
	return FALSE

/proc/all_have_erp_pref(list/mobs, pref_type)
	for(var/mob/M in mobs)
		if(!M.has_erp_pref(pref_type))
			return FALSE
	return TRUE

/mob/living/proc/has_kink(kink_name)
	refresh_erp_preference_cache()
	var/list/kink_prefs = cached_erp_preferences?["kinks"]
	if(!kink_prefs || !kink_prefs[kink_name])
		return FALSE
	return kink_prefs[kink_name]["enabled"]

/mob/living/proc/is_disconnected_player_erp_body()
	if(client)
		return FALSE
	if(!mind)
		return FALSE
	return !!(mind.key || mind.cached_erp_preferences || cached_erp_preferences)

/mob/living/proc/allows_player_erp_while_disconnected()
	if(!is_disconnected_player_erp_body())
		return TRUE
	return get_cached_erp_pref(/datum/erp_preference/boolean/allow_player_erp_when_disconnected) == TRUE

/datum/mind
	var/tmp/list/cached_erp_preferences
	var/tmp/cached_erp_preferences_revision = -1

/datum/mind/proc/cache_erp_preferences_from_prefs(datum/preferences/prefs)
	if(!prefs?.erp_preferences)
		return FALSE
	set_cached_erp_preferences(prefs.erp_preferences, prefs.erp_preferences_revision)
	return TRUE

/datum/mind/proc/set_cached_erp_preferences(list/preferences, revision = -1)
	cached_erp_preferences = islist(preferences) ? deep_copy_list_alt(preferences) : null
	cached_erp_preferences_revision = revision
	if(isliving(current))
		var/mob/living/current_living = current
		current_living.set_cached_erp_preferences(cached_erp_preferences, cached_erp_preferences_revision)

/mob/living
	var/list/attached_sex_toys = list()
	var/tmp/list/cached_erp_preferences
	var/tmp/erp_preferences_revision_seen = -1
	var/tmp/cached_horny_mob_pref_flags = NONE
	var/tmp/cached_horny_mob_family_flags = NONE
	var/tmp/cached_allow_belly_inflation = null
	var/tmp/cached_nonmatching_horny_mobs_are_nonlethal = null

	///npc organs to use
	var/ball_organ = /obj/item/organ/genitals/filling_organ/testicles
	var/ball_min = MIN_TESTICLES_SIZE
	var/ball_max = MAX_TESTICLES_SIZE
	var/breast_organ = /obj/item/organ/genitals/filling_organ/breasts
	var/breast_min = MIN_BREASTS_SIZE
	var/breast_max = MAX_BREASTS_SIZE
	var/ass_organ = /obj/item/organ/genitals/butt
	var/ass_min = MIN_BUTT_SIZE
	var/ass_max = MAX_BUTT_SIZE
	var/penis_organ = /obj/item/organ/genitals/penis
	var/penis_min = MIN_PENIS_SIZE
	var/penis_max = MAX_PENIS_SIZE
	var/butt_organ = /obj/item/organ/genitals/butt
	var/butt_min = MIN_BUTT_SIZE
	var/butt_max = MAX_BUTT_SIZE
	var/vagina_organ = /obj/item/organ/genitals/filling_organ/vagina
	var/show_genitals = FALSE
	var/mouth_blocked = FALSE

	COOLDOWN_DECLARE(npc_sneak_detect_cd)

/datum/component/gender_potion_genital_backup
	var/original_gender
	var/list/stored_organs = list()
	var/list/stored_zones = list()

/datum/component/gender_potion_genital_backup/Initialize(original_gender)
	if(!isliving(parent))
		return COMPONENT_INCOMPATIBLE
	src.original_gender = original_gender

/datum/component/gender_potion_genital_backup/Destroy(force = FALSE)
	QDEL_LIST(stored_organs)
	stored_zones = null
	return ..()

/datum/component/gender_potion_genital_backup/proc/store_current_genitals(mob/living/owner)
	if(!owner)
		return FALSE
	for(var/organ_slot in owner.get_gender_potion_genital_slots())
		for(var/obj/item/organ/genitals/organ as anything in owner.getorganslotlist(organ_slot))
			if(QDELETED(organ))
				continue
			stored_organs += organ
			stored_zones[organ] = organ.current_zone
			owner.remove_gender_potion_genital_organ(organ, TRUE)
	return TRUE

/datum/component/gender_potion_genital_backup/proc/restore_genitals(mob/living/owner)
	if(!owner)
		return FALSE
	owner.clear_gender_potion_genitals()
	for(var/obj/item/organ/genitals/organ as anything in stored_organs)
		if(QDELETED(organ))
			continue
		var/saved_zone = null
		if(stored_zones)
			saved_zone = stored_zones[organ]
		DISABLE_BITFIELD(organ.organ_flags, ORGAN_CUT_AWAY)
		organ.Insert(owner, special = TRUE, drop_if_replaced = FALSE, new_zone = saved_zone)
	stored_organs = null
	stored_zones = null
	qdel(src)
	return TRUE

/mob/living/proc/get_gender_potion_genital_slots()
	var/static/list/gender_potion_genital_slots = list(
		ORGAN_SLOT_PENIS,
		ORGAN_SLOT_TESTICLES,
		ORGAN_SLOT_BREASTS,
		ORGAN_SLOT_VAGINA
	)
	return gender_potion_genital_slots

/mob/living/proc/remove_gender_potion_genital_organ(obj/item/organ/genitals/organ, preserve = FALSE)
	if(!organ || organ.owner != src)
		return FALSE
	organ.Remove(src, special = TRUE)
	if(preserve)
		organ.moveToNullspace()
		DISABLE_BITFIELD(organ.organ_flags, ORGAN_CUT_AWAY)
		STOP_PROCESSING(SSobj, organ)
	else
		qdel(organ)
	return TRUE

/mob/living/proc/clear_gender_potion_genitals()
	for(var/organ_slot in get_gender_potion_genital_slots())
		for(var/obj/item/organ/genitals/organ as anything in getorganslotlist(organ_slot))
			remove_gender_potion_genital_organ(organ)

/mob/living/proc/give_gender_potion_genitals_for_gender(target_gender)
	if(target_gender == MALE)
		var/obj/item/organ/genitals/filling_organ/testicles/testicles = getorganslot(ORGAN_SLOT_TESTICLES)
		if(!testicles)
			if(!show_genitals)
				testicles = new /obj/item/organ/genitals/filling_organ/testicles/invisible
			else
				testicles = new ball_organ
			testicles.organ_size = rand(ball_min, ball_max)
			testicles.Insert(src, TRUE)

		var/obj/item/organ/genitals/penis/penis = getorganslot(ORGAN_SLOT_PENIS)
		if(!penis)
			if(!show_genitals)
				penis = new /obj/item/organ/genitals/penis
			else
				penis = new penis_organ
			penis.organ_size = rand(penis_min, penis_max)
			penis.Insert(src, TRUE)

	else if(target_gender == FEMALE)
		var/obj/item/organ/genitals/filling_organ/breasts/breasts = getorganslot(ORGAN_SLOT_BREASTS)
		if(!breasts)
			if(!show_genitals)
				breasts = new /obj/item/organ/genitals/filling_organ/breasts
			else
				breasts = new breast_organ
			breasts.organ_size = rand(breast_min, breast_max)
			breasts.Insert(src, TRUE)

		var/obj/item/organ/genitals/filling_organ/vagina/vagina = getorganslot(ORGAN_SLOT_VAGINA)
		if(!vagina)
			if(!show_genitals)
				vagina = new /obj/item/organ/genitals/filling_organ/vagina
			else
				vagina = new vagina_organ
			vagina.Insert(src, TRUE)

	if(iscarbon(src))
		var/mob/living/carbon/carbon_owner = src
		carbon_owner.update_body_parts(TRUE)
	return TRUE

/mob/living/proc/apply_gender_potion_genital_change(old_gender)
	if(gender != MALE && gender != FEMALE)
		return FALSE
	var/datum/component/gender_potion_genital_backup/backup = GetComponent(/datum/component/gender_potion_genital_backup)
	if(backup && gender == backup.original_gender)
		return backup.restore_genitals(src)

	if(!backup)
		backup = AddComponent(/datum/component/gender_potion_genital_backup, old_gender)
		if(!backup)
			return FALSE
		backup.store_current_genitals(src)
	else
		clear_gender_potion_genitals()

	give_gender_potion_genitals_for_gender(gender)
	return TRUE

/mob/living/Initialize()
	. = ..()
	refresh_erp_preference_cache()
	if(ai_controller)
		var/datum/ai_planning_subtree/horny/hornybehavior = locate() in ai_controller.planning_subtrees
		if(hornybehavior && !GetComponent(/datum/component/arousal))
			AddComponent(/datum/component/arousal)

/mob/living/proc/invalidate_erp_preference_cache()
	erp_preferences_revision_seen = -1
	cached_erp_preferences = null
	refresh_erp_preference_cache()

/mob/living/proc/rebuild_cached_erp_preference_shortcuts()
	cached_horny_mob_pref_flags = NONE
	cached_horny_mob_family_flags = NONE
	cached_allow_belly_inflation = null
	cached_nonmatching_horny_mobs_are_nonlethal = null
	if(!cached_erp_preferences)
		return
	var/datum/erp_preference/boolean/allow_belly_inflation/belly_pref = new
	cached_allow_belly_inflation = belly_pref.get_value_from_list(cached_erp_preferences)
	var/datum/erp_preference/boolean/nonmatching_horny_mobs_are_nonlethal/nonlethal_pref = new
	cached_nonmatching_horny_mobs_are_nonlethal = nonlethal_pref.get_value_from_list(cached_erp_preferences)
	cached_horny_mob_pref_flags = cached_erp_preferences[/datum/erp_preference/bitflag/horny_mobs] || NONE
	var/allowed_families = cached_erp_preferences[/datum/erp_preference/bitflag/horny_mob_types]
	if(isnull(allowed_families))
		allowed_families = HORNY_MOB_TYPE_ALL
	cached_horny_mob_family_flags = allowed_families

/mob/living/proc/set_cached_erp_preferences(list/preferences, revision = -1)
	cached_erp_preferences = islist(preferences) ? deep_copy_list_alt(preferences) : null
	erp_preferences_revision_seen = revision
	rebuild_cached_erp_preference_shortcuts()

/mob/living/proc/cache_erp_preferences_from_prefs(datum/preferences/prefs)
	if(!prefs?.erp_preferences)
		return FALSE
	if(mind?.current == src)
		return mind.cache_erp_preferences_from_prefs(prefs)
	set_cached_erp_preferences(prefs.erp_preferences, prefs.erp_preferences_revision)
	return TRUE

/mob/living/proc/refresh_erp_preference_cache()
	var/datum/preferences/prefs = client?.prefs
	if(prefs?.erp_preferences)
		var/current_revision = prefs.erp_preferences_revision
		if(erp_preferences_revision_seen == current_revision && cached_erp_preferences)
			return
		cache_erp_preferences_from_prefs(prefs)
		return

	if(mind?.cached_erp_preferences)
		if(erp_preferences_revision_seen == mind.cached_erp_preferences_revision && cached_erp_preferences)
			return
		set_cached_erp_preferences(mind.cached_erp_preferences, mind.cached_erp_preferences_revision)
		return

	if(!cached_erp_preferences)
		rebuild_cached_erp_preference_shortcuts()

/mob/living/proc/get_cached_erp_pref(pref_type)
	if(!ispath(pref_type, /datum/erp_preference))
		return FALSE
	refresh_erp_preference_cache()
	if(!cached_erp_preferences)
		return FALSE
	var/datum/erp_preference/pref = new pref_type()
	return pref.get_value_from_list(cached_erp_preferences)

/mob/living/proc/get_cached_allow_belly_inflation()
	refresh_erp_preference_cache()
	return cached_allow_belly_inflation == TRUE

/mob/living/proc/get_cached_nonmatching_horny_mobs_are_nonlethal()
	refresh_erp_preference_cache()
	return cached_nonmatching_horny_mobs_are_nonlethal == TRUE

/mob/living/proc/get_cached_horny_mob_pref_flags()
	refresh_erp_preference_cache()
	return cached_horny_mob_pref_flags

/mob/living/proc/get_cached_horny_mob_family_flags()
	refresh_erp_preference_cache()
	return cached_horny_mob_family_flags

/mob/living/proc/give_genitals()
	var/mob/living/user = src
	var/obj/item/organ/genitals/filling_organ/anus/anus = user.getorganslot(ORGAN_SLOT_ANUS)
	if(!anus)
		anus = new /obj/item/organ/genitals/filling_organ/anus
		anus.Insert(user, TRUE)
	if(gender == MALE)
		var/obj/item/organ/genitals/filling_organ/testicles/testicles = user.getorganslot(ORGAN_SLOT_TESTICLES)
		if(!testicles)
			if(!show_genitals)
				testicles = new /obj/item/organ/genitals/filling_organ/testicles/invisible
			else
				testicles = new ball_organ
			testicles.organ_size = rand(ball_min, ball_max)
			testicles.Insert(user, TRUE)
		var/obj/item/organ/genitals/penis/penis = user.getorganslot(ORGAN_SLOT_PENIS)
		if(!penis)
			if(!show_genitals)
				penis = new /obj/item/organ/genitals/penis
			else
				penis = new penis_organ
			penis.organ_size = rand(penis_min, penis_max)
			penis.Insert(user, TRUE)
	if(gender == FEMALE)
		var/obj/item/organ/genitals/butt/buttie = user.getorganslot(ORGAN_SLOT_BUTT)
		if(!buttie)
			if(!show_genitals)
				buttie = new /obj/item/organ/genitals/butt/invisible
			else
				buttie = new butt_organ
			buttie.organ_size = rand(butt_min, butt_max)
			buttie.Insert(user, TRUE)
		var/obj/item/organ/genitals/filling_organ/breasts/breasts = user.getorganslot(ORGAN_SLOT_BREASTS)
		if(!breasts)
			if(!show_genitals)
				breasts = new /obj/item/organ/genitals/filling_organ/breasts
			else
				breasts = new breast_organ
			breasts.organ_size = rand(breast_min,breast_max)
			breasts.Insert(user, TRUE)
		var/obj/item/organ/genitals/filling_organ/vagina/vagina = user.getorganslot(ORGAN_SLOT_VAGINA)
		if(!vagina)
			if(!show_genitals)
				vagina = new /obj/item/organ/genitals/filling_organ/vagina
			else
				vagina = new vagina_organ
			vagina.Insert(user, TRUE)
		if(prob(5)) //5 chance to be dickgirl.
			var/obj/item/organ/genitals/filling_organ/testicles/testicles = user.getorganslot(ORGAN_SLOT_TESTICLES)
			if(!testicles)
				if(!show_genitals)
					testicles = new /obj/item/organ/genitals/filling_organ/testicles/invisible
				else
					testicles = new ball_organ
				testicles.organ_size = rand(ball_min, ball_max)
				testicles.Insert(user, TRUE)
			var/obj/item/organ/genitals/penis/penis = user.getorganslot(ORGAN_SLOT_PENIS)
			if(!penis)
				if(!show_genitals)
					penis = new /obj/item/organ/genitals/penis
				else
					penis = new penis_organ
				penis.organ_size = rand(penis_min, penis_max)
				penis.Insert(user, TRUE)
	if(iscarbon(user))
		var/mob/living/carbon/carbon_user = user
		carbon_user.update_body_parts()
	return TRUE

/mob/living/proc/lose_virginity()
	return

/mob/living/carbon/human/lose_virginity()
	virginity = FALSE

/mob/living/proc/adjacent_or_closet(atom/neighbor)
	if(istype(loc, /obj/structure/closet) || istype(loc, /obj/structure/handcart) || istype(neighbor.loc, /obj/structure/closet) || istype(neighbor.loc, /obj/structure/handcart)) // within container
		return loc == neighbor.loc
	return Adjacent(neighbor)

/mob/living/proc/check_closet(atom/neighbor)
	if(istype(loc, /obj/structure/closet) || istype(loc, /obj/structure/handcart) || istype(neighbor.loc, /obj/structure/closet) || istype(neighbor.loc, /obj/structure/handcart)) // within container
		return loc == neighbor.loc
	else
		return FALSE
